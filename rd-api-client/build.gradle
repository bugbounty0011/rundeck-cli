import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
    }
}
plugins{
    id "org.dvaske.gradle.git-build-info" version "0.9"
    id 'com.github.gmazzo.buildconfig' version '3.0.3'
}

apply plugin: 'idea'
apply plugin: 'groovy'

group = 'org.rundeck.api'

ext.publishName = "Rundeck API Library ${project.version}"
ext.publishDescription = project.description ?: 'Rundeck API Library'
ext.githubSlug = 'rundeck/rundeck-cli'
ext.developers = [
    [id: 'gschueler', name: 'Greg Schueler', email: 'greg@rundeck.com']
]

/**
 * Generates java source with embedded version info
 */
buildConfig {
    className "Version"
    packageName 'org.rundeck.client'

    buildConfigField('String', 'NAME', "\"rundeck-cli\"")
    buildConfigField('String', 'VERSION', "\"${project.version}\"")
    buildConfigField('String', 'GIT_COMMIT', "\"${project.gitCommit}\"")
    buildConfigField('String', 'GIT_BRANCH', "\"${project.gitBranch}\"")
    buildConfigField('String', 'GIT_DESCRIPTION', "\"${project.gitDescribeInfo}\"")
    buildConfigField('String', 'BUILD_DATE', "\"${new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ssX").format(new Date())}\"")
}


dependencies {

    compileOnly libs.lombok
    annotationProcessor libs.lombok

    api libs.bundles.retrofit

    testImplementation libs.retrofitMock
    testImplementation libs.okhttpMockwebserver
    testImplementation libs.bundles.test
}


apply from: "${rootDir}/gradle/publishing.gradle"
