import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
    }
}
plugins{
    id "org.dvaske.gradle.git-build-info" version "0.9"
    id 'com.github.gmazzo.buildconfig' version '3.0.3'
}

apply plugin: 'idea'
apply plugin: 'groovy'

group = 'org.rundeck.api'

ext.publishName = "Rundeck API Library ${project.version}"
ext.publishDescription = project.description ?: 'Rundeck API Library'
ext.githubSlug = 'rundeck/rundeck-cli'
ext.developers = [
    [id: 'gschueler', name: 'Greg Schueler', email: 'greg@rundeck.com']
]

/**
 * Generates java source with embedded version info
 */
buildConfig {
    className "Version"
    packageName 'org.rundeck.client'

    buildConfigField('String', 'NAME', "\"rundeck-cli\"")
    buildConfigField('String', 'VERSION', "\"${project.version}\"")
    buildConfigField('String', 'GIT_COMMIT', "\"${project.gitCommit}\"")
    buildConfigField('String', 'GIT_BRANCH', "\"${project.gitBranch}\"")
    buildConfigField('String', 'GIT_DESCRIPTION', "\"${project.gitDescribeInfo}\"")
    buildConfigField('String', 'BUILD_DATE', "\"${new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ssX").format(new Date())}\"")
}


dependencies {

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    api "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    api "com.squareup.retrofit2:converter-jackson:${retrofitVersion}"
    api "com.squareup.retrofit2:converter-jaxb:${retrofitVersion}"
    api "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"
    api "com.squareup.okhttp3:okhttp-urlconnection:${okhttpVersion}"
    api 'com.sun.xml.bind:jaxb-impl:3.0.2'


    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation "com.squareup.retrofit2:retrofit-mock:${retrofitVersion}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"
    testImplementation "org.codehaus.groovy:groovy-all:3.0.10"
    testImplementation "org.spockframework:spock-core:2.1-groovy-3.0"
}


apply from: "${rootDir}/gradle/publishing.gradle"
